/*
 *     Copyright 2018 Jonathan Merritt 11R00TT00R11@GMAIL.COM
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

ext {
  def local = new Properties()
  local.load(file("local.properties").newDataInputStream())
  developer = [
      name  : "jonathanmerritt",
      email : "11R00TT00RR11@GMAIL.COM",
      pass  : local.getProperty("developer.pass"),
      key   : local.getProperty("developer.key")
  ]

  repository = [url : "https://github.com/$developer.name/$rootProject.name"]
  repository.licenses = ["Apache-2.0"]

  androids = [
      compileSdk : 27,
      buildTools : "27.0.3",
      minSdk     : 17
  ]

  dependencys = [javax : "javax.inject:javax.inject:1"]

  def support = [
      group   : "com.android.support",
      version : "27.0.2"
  ]
  dependencys.supports = [
      appcompatV7 : "$support.group:appcompat-v7:$support.version",
      supportV4   : "$support.group:support-v4:$support.version",
      design      : "$support.group:design:$support.version"
  ]

  def constraint = "1.1.0-beta5"
  dependencys.supports.constraints = [
      "${support.group}.constraint:constraint-layout:$constraint",
      "${support.group}.constraint:constraint-layout-solver:$constraint"
  ]

  dependencys.supports.tests = [
      runner   : "${support.group}.test:runner:1.0.1",
      espresso : "${support.group}.test.espresso:espresso-core:3.0.1"
  ]

  def dagger = [
      group   : "com.google.dagger",
      version : "2.14.1"
  ]
  dependencys.daggers = [
      android        : "$dagger.group:dagger-android:$dagger.version",
      androidSupport : "$dagger.group:dagger-android-support:$dagger.version"
  ]
  dependencys.daggers.annotations = [
      compiler         : "$dagger.group:dagger-compiler:$dagger.version",
      androidProcessor : "$dagger.group:dagger-android-processor:$dagger.version"
  ]

  dependencys.tests = [junit : "junit:junit:4.12"]
}

allprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  group = "com.github.jonathanmerritt.androidscabbard"
  version = it.name.contains("app") ? "0.0.1" : "0.0.7"

  ext {
    library = [
        description : "${rootProject.name}s' $name library",
        url         : "$repository.url/tree/master/$name"
    ]

    androids += [
        versionCode : version.replace(".", "").concat("git rev-list --count HEAD".execute().text.trim())
            .concat(new Date().format("MMyy")).toBigInteger()
    ]
  }
}