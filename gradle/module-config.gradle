/*
 *     Copyright 2018 Jonathan Merritt 11R00TT00R11@GMAIL.COM
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

repositories {
  google()
  jcenter()
  mavenCentral()
}

def isApp = plugins.hasPlugin('com.android.application')
ext {
  rep.version = [
      name : isApp ? "0.0.1" : "0.0.8"
  ]
  rep.version += [
      code : rep.version.name.replace(".", "").concat(new Date().format("MMddyy")).toBigInteger()
  ]

  rep.lib = [
      name : name
  ]
  rep.lib += [
      url  : "$rep.url/tree/master/$rep.lib.name",
      desc : "${rep.name}s' $rep.lib.name library"
  ]
}

android {
  def compile = 27
  compileSdkVersion compile
  buildToolsVersion "27.0.3"

  defaultConfig {
    minSdkVersion 17
    targetSdkVersion compile

    versionCode rep.version.code
    versionName rep.version.name

    if (isApp) {
      applicationId rep.id
      testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
  }

  buildTypes {
    release {
      if (isApp) {
        minifyEnabled false
        proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      } else {
        postprocessing {
          removeUnusedCode false
          removeUnusedResources false
          obfuscate false
          optimizeCode false
          proguardFile 'proguard-rules.pro'
        }
      }
    }
  }

  if (isApp) {
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }
  }
}